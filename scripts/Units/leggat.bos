
#include "../recoil_common_includes.h"

piece  flare, turret, base, sleeve, barrel, rwheel, lwheel, laxle, raxle, smoke1, smoke2;

static-var  restore_delay, wpn1_lasthead;

// Signal definitions
#define SIGNAL_AIM1 256
#define SIGNAL_MOVE 1
#define SIGNAL_SHOOT1 16


#define VD_PIECE1 smoke1
#define VD_PIECE2 smoke2
#define VD_AMPHIBIOUS
#define VD_DIRTCEG 1024 + 1
#include "../vehicle_dirt_cegs.h"


#define BASEPIECE base
#define HITSPEED <55.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 6
#define MAXTILT 50

RockUnit(anglex, anglez)
{
	turn base to x-axis anglex speed <50.0>;
	turn base to z-axis <0> - anglez speed <50.0>;
	wait-for-turn base around z-axis;
	wait-for-turn base around x-axis;
	turn base to z-axis <0.0> speed <20.0>;
	turn base to x-axis <0.0> speed <20.0>;
}

Create()
{
	hide smoke1;
	hide flare;
	hide smoke2;
	restore_delay = 200;
}

SetMaxReloadTime(reloadMS)
{
	restore_delay = reloadMS * 2;
}

StartMoving(reversing)
{
	spin lwheel around x-axis speed <110> accelerate <5>;
	spin rwheel around x-axis speed <110> accelerate <5>;
  
	signal SIGNAL_MOVE;
	set-signal-mask SIGNAL_MOVE;
	start-script Vehicle_Dirt_Ceg();
}


StopMoving()
{
	signal SIGNAL_MOVE;
	stop-spin lwheel around x-axis;
	stop-spin rwheel around x-axis;
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	turn turret to y-axis <0.0> speed <50.0>;
	turn sleeve to x-axis <0.0> speed <25.0>;
	spin barrel around z-axis speed <600.0>;
	sleep restore_delay;
	spin barrel around z-axis speed <300.0>;
	sleep (restore_delay / 2);
	spin barrel around z-axis speed <150.0>;
	sleep (restore_delay / 4);
	stop-spin barrel around z-axis;
	wpn1_lasthead = 0xbadface;
	set-signal-mask 0;
}
SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

AimWeapon1(heading, pitch)
{
	signal SIGNAL_AIM1;
	set-signal-mask SIGNAL_AIM1;
	turn turret to y-axis heading speed <120.0>;
	turn sleeve to x-axis <0.0> - pitch speed <65.0>;
	if ((wpn1_lasthead == 0xbadface) OR ABSOLUTE_GREATER_THAN(WRAPDELTA(heading - wpn1_lasthead), <7>))
	{
		wpn1_lasthead = 0xbadface;
		wait-for-turn turret around y-axis;
		wait-for-turn sleeve around x-axis;
	}
	wpn1_lasthead = heading;
	start-script RestoreAfterDelay();
	return (1);
}

FireWeapon1()
{
	spin barrel around z-axis speed <900.0>;
	return (1);
}

Shot1(zero)
{
	signal SIGNAL_SHOOT1;
	set-signal-mask SIGNAL_SHOOT1;
	emit-sfx 1024 + 0 from flare;
	return(1);
}

QueryWeapon1(pieceIndex)
{
		pieceIndex = flare;
}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = sleeve;
}



Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode raxle type BITMAPONLY | NOHEATCLOUD;
		explode rwheel type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode raxle type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rwheel type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode raxle type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rwheel type SMOKE | FALL | NOHEATCLOUD;
		explode turret type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode raxle type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rwheel type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode sleeve type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode barrel type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
	return corpsetype;
}
