
#include "exptype.h"

piece  base,fan,top,column;

static-var  resourceRate, statechg_DesiredState, statechg_StateChanging, Stunned;


Go()
{
	if (Stunned) {
		return(0);
	}
	set ARMORED to 0;
	move fan to y-axis [3.000000] speed [10.000000];
	wait-for-move fan along y-axis;
	spin fan around y-axis speed resourceRate accelerate 90;
}

Stop()
{
	stop-spin fan around y-axis decelerate 180;
	wait-for-turn fan around y-axis;
	move fan to y-axis [0.000000] speed [10.000000];
	set ARMORED to 1;
}

InitState()
{
	statechg_DesiredState = TRUE;
	statechg_StateChanging = FALSE;
}

SetStunned(State)
{
	Stunned = State;
	if (State) {
	    call-script Stop();
	}else{
	    if (statechg_DesiredState == 0) {
	        call-script Go();
	    }
	}
}

RequestState(requestedstate, currentstate)
{
	if( statechg_StateChanging )
	{
		statechg_DesiredState = requestedstate;
		return (0);
	}
	statechg_StateChanging = TRUE;
	currentstate = statechg_DesiredState;
	statechg_DesiredState = requestedstate;
	while( statechg_DesiredState != currentstate )
	{
		if( statechg_DesiredState == 0 )
		{
			call-script Go();
			currentstate = 0;
		}
		if( statechg_DesiredState == 1 )
		{
			call-script Stop();
			currentstate = 1;
		}
	}
	statechg_StateChanging = FALSE;
}

Create()
{
	resourceRate = 1500;
	call-script InitState();
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 250;
	}
}

#define SMOKEPIECE base

Activate()
{
	start-script RequestState(0);
}

Deactivate()
{
	start-script RequestState(1);
}

SetSpeed(windOrMetal)
{
	resourceRate = windOrMetal * 60;
}



Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode fan type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode base type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode fan type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode fan type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode base type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode fan type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
