
#include "exptype.h"

piece  base, disc;

static-var  resourceRate, isOn, statechg_DesiredState, statechg_StateChanging;
static-var  Stunned;

// Signal definitions
#define SIGNAL_TURNON 4


Go()
{
	if (Stunned) {
		return(0);
	}
	spin disc around y-axis speed resourceRate accelerate 90;
	set ARMORED to 0;
	isOn = 1;
}

Stop()
{
	stop-spin disc around y-axis decelerate 180;
	isOn = 0;
	set ARMORED to 1;

}

InitState()
{
	statechg_DesiredState = TRUE;
	statechg_StateChanging = FALSE;
}

RequestState(requestedstate, currentstate)
{
	if( statechg_StateChanging )
	{
		statechg_DesiredState = requestedstate;
		return (0);
	}
	statechg_StateChanging = TRUE;
	currentstate = statechg_DesiredState;
	statechg_DesiredState = requestedstate;
	while( statechg_DesiredState != currentstate )
	{
		if( statechg_DesiredState == 0 )
		{
			call-script Go();
			currentstate = 0;
		}
		if( statechg_DesiredState == 1 )
		{
			call-script Stop();
			currentstate = 1;
		}
	}
	statechg_StateChanging = FALSE;
}

Create()
{
	isOn = 0;
	resourceRate = 0;
	call-script InitState();
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 1000;
	}
	set ARMORED to 1;
}

SetStunned(State)
{
	Stunned = State;
	if (Stunned) {
	    call-script Stop();
	} else {
	    call-script Go();
	}
}

Activate()
{
	start-script RequestState(0);
	signal SIGNAL_TURNON;
	set-signal-mask SIGNAL_TURNON;
	while( TRUE )
	{
		if( isOn )
		{
			emit-sfx 259 from disc;
		}
		sleep 250;
	}
}

Deactivate()
{
	start-script RequestState(1);
	signal SIGNAL_TURNON;
}



SetSpeed(windOrMetal)
{
	resourceRate = windOrMetal * 10;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode disc type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode disc type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode disc type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode disc type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
