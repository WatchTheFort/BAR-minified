#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  flare1, flare2, turret, base, sleeves, barrel1, barrel2;

static-var  restore_delay, gun_1, fire_count;
static-var  last_primary_heading,last_primary_pitch;

// Signal definitions
#define SIG_AIM				2
#define SIG_FIRE1			4



#define BASEPIECE base
#define HITSPEED <85.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 1
#define MAXTILT 100

#include "unit_hitbyweaponid_and_smoke.h"


Create()
{
	hide flare1;
	hide flare2;
	hide flare1;
	hide flare2;
	gun_1 = flare1;
	fire_count = 0;
	// set last headings and pitches to large negative numbers
	last_primary_heading = -1000000;
	last_primary_pitch = -1000000;
	restore_delay = 3000;
}

SetMaxReloadTime(Func_Var_1)
{
	restore_delay = Func_Var_1 * 2;
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	turn turret to y-axis <0.000000> speed <90.016484>;
	turn sleeves to x-axis <0.000000> speed <50.005495>;
	// set last headings and pitches to large negative numbers
	last_primary_heading = -1000000;
	last_primary_pitch = -1000000;
}
SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	// values in <> are degrees per second
	// otherwise angles are in COB angular unit (cau) - There are 65536 cau in a circle
	// In general, 6 cau per frame ~= 1 degree per second
	turn turret to y-axis heading speed <250.000000>;
	turn sleeves to x-axis <0.000000> - pitch speed <240.000000>;
	// if the turret cannot turn to its new heading in one frame, wait for turn
	if ( (get ABS(last_primary_heading-heading)>1500) OR (get ABS(last_primary_pitch-pitch)>1440) )
	{
		// seems to take 3 frames for wait-for-turn to process
		wait-for-turn turret around y-axis;
		wait-for-turn sleeves around x-axis;
	}
	last_primary_heading=heading;
	last_primary_pitch=pitch;
	start-script RestoreAfterDelay();
	return (1);
}

RestoreBarrel1()
{
	set-signal-mask 0;	//make sure shots from other barrel do not interrupt current barrel movement
	sleep 100;
	move barrel1 to z-axis [0.000000] speed [5.000000];
}

RestoreBarrel2()
{
	set-signal-mask 0;	//make sure shots from other barrel do not interrupt current barrel movement
	sleep 100;
	move barrel2 to z-axis [0.000000] speed [5.000000];
}

Shot1(zero)
{
	signal SIG_FIRE1;
	set-signal-mask SIG_FIRE1;	
	
	if(fire_count==0)
	{
		if( gun_1 == 0 )
		{
			move barrel1 to z-axis [-0.4666] speed [300.000000];
			emit-sfx 1024+0 from flare1;
		}
		if( gun_1 == 1 )
		{
			move barrel2 to z-axis [-0.4666] speed [300.000000];
			emit-sfx 1024 + 0 from flare2;
		}
	}
	if(fire_count==1)
	{
		if( gun_1 == 0 )
		{
			move barrel1 to z-axis [-0.9333] speed [300.000000];
			emit-sfx 1024+0 from flare1;
		}
		if( gun_1 == 1 )
		{
			move barrel2 to z-axis [-0.9333] speed [300.000000];
			emit-sfx 1024 + 0 from flare2;
		}
	}
	if(fire_count==2)
	{
		if( gun_1 == 0 )
		{
			move barrel1 to z-axis [-1.399994] speed [300.000000];
			emit-sfx 1024+0 from flare1;
			start-script RestoreBarrel1();
			//wait-for-move barrel1 along z-axis;
			//move barrel1 to z-axis [0.000000] speed [5.000000];
		}
		if( gun_1 == 1 )
		{
			move barrel2 to z-axis [-1.399994] speed [300.000000];
			emit-sfx 1024 + 0 from flare2;
			start-script RestoreBarrel2();
			//wait-for-move barrel2 along z-axis;
			//move barrel2 to z-axis [0.000000] speed [5.000000];
		}
	}
	sleep 32;
	++fire_count;
	if(fire_count>=3)
	{
		fire_count=0;
		gun_1 = !gun_1; // swap emit barrel at end of burst
		// wait, then move barrels back to neutral
		//wait-for-move barrel1 along z-axis;
		//wait-for-move barrel2 along z-axis;
		//move barrel1 to z-axis [0.000000] speed [5.000000];
		//move barrel2 to z-axis [0.000000] speed [5.000000];
	}
	return(1);
}

FirePrimary()
{
}

AimFromPrimary(piecenum)
{
	piecenum = turret;
}

QueryPrimary(piecenum)
{
	piecenum = gun_1;
}

SweetSpot(piecenum)
{
	piecenum = turret;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret type BITMAPONLY | NOHEATCLOUD;
		explode sleeves type BITMAPONLY | NOHEATCLOUD;
		explode barrel1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode flare1 type BITMAPONLY | NOHEATCLOUD;
		//explode barrel2 type BITMAPONLY | NOHEATCLOUD;
		//explode flare2 type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode sleeves type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrel1 type FALL | NOHEATCLOUD;
		//explode flare1 type FALL | NOHEATCLOUD;
		//explode barrel2 type FALL | NOHEATCLOUD;
		//explode flare2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode sleeves type SMOKE | FALL | NOHEATCLOUD;
		explode barrel1 type SMOKE | FALL | NOHEATCLOUD;
		//explode flare1 type SMOKE | FALL | NOHEATCLOUD;
		explode barrel2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode flare2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode sleeves type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode barrel1 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		//explode flare1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrel2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		//explode flare2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
