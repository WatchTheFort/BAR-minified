
#include "../recoil_common_includes.h"

piece  pelvis, body, rthigh, lthigh, lleg, rleg, rfoot, lfoot, lsleeve, rsleeve, lbarrel, luparm, ruparm, 
	 rbarrel, lflare, rflare, aimx1, aimy1;

static-var  isMoving, isAiming, restore_delay, wpn1_lasthead, miniguncount, animspeed;

// Signal definitions
#define SIGNAL_AIM1 256
#define SIGNAL_SHOOT1 16


walk()
{
	//step 1, foot back a bit
	
	turn rthigh to x-axis <22> speed <220.0>/animspeed;
	turn rleg to x-axis <10> speed <100.0>/animspeed;
	turn rfoot to x-axis <-22> speed <220.0>/animspeed;
	turn lthigh to x-axis <-45> speed <770.0>/animspeed;
	turn lleg to x-axis <0> speed <280.0>/animspeed;
	turn lfoot to x-axis <0> speed <510.0>/animspeed;
	turn pelvis to z-axis <2> speed <40>/animspeed;
	move pelvis to y-axis [1] speed [10.0]/animspeed;
	
	sleep 100*animspeed;
	
	
	//step 2, foot fully back, heel lifted
	
	
	//turn rthigh to z-axis <-2> now;
	turn rleg to x-axis <30> speed <200.0>/animspeed;
	turn lthigh to x-axis <-60> speed <150.0>/animspeed;
	turn lleg to x-axis <35> speed <350.0>/animspeed;
	turn lfoot to x-axis <20> speed <200.0>/animspeed;
	turn pelvis to z-axis <4> speed <20>/animspeed;
	move pelvis to y-axis [1] speed [20.0]/animspeed;
	
	sleep 100*animspeed;
	
	//step 3, foot lifted, knee bent
	turn rthigh to x-axis <15> speed <70.0>/animspeed;
	turn rleg to x-axis <0> speed <300.0>/animspeed;
	turn rfoot to x-axis <-51> speed <290.0>/animspeed;
	turn lthigh to x-axis <-30> speed <300.0>/animspeed;
	turn lleg to x-axis <25> speed <100.0>/animspeed;
	turn lfoot to x-axis <5> speed <150.0>/animspeed;
	turn pelvis to z-axis <2> speed <20>/animspeed;
	move pelvis to y-axis [1] now;
	
	sleep 100*animspeed;
	
	//step 4 foot lifted, knee extended
	
	turn lthigh to x-axis <22> speed <520.0>/animspeed;
	turn lleg to x-axis <10> speed <350.0>/animspeed;
	turn lfoot to x-axis <-22> speed <270.0>/animspeed;
	turn rthigh to x-axis <-45> speed <600.0>/animspeed;
	turn rleg to x-axis <0> speed <10.0>/animspeed;
	turn rfoot to x-axis <0> speed <510.0>/animspeed;
	turn pelvis to z-axis <-2> speed <40>/animspeed;
	move pelvis to y-axis [1] speed [10.0]/animspeed;
	
	sleep 100*animspeed;
	
	//step 5, foot on ground, knee extended
	
	
	turn lleg to x-axis <30> speed <200.0>/animspeed;
	turn rthigh to x-axis <-60> speed <150.0>/animspeed;
	turn rleg to x-axis <35> speed <350.0>/animspeed;
	turn rfoot to x-axis <20> speed <200.0>/animspeed;
	turn pelvis to z-axis <-4> speed <20>/animspeed;
	move pelvis to y-axis [1] speed [20.0]/animspeed;
	
	sleep 100*animspeed;
	
	turn lthigh to x-axis <15> speed <70.0>/animspeed;
	turn lleg to x-axis <0> speed <300.0>/animspeed;
	turn lfoot to x-axis <-51> speed <290.0>/animspeed;
	turn rthigh to x-axis <-30> speed <300.0>/animspeed;
	turn rleg to x-axis <25> speed <100.0>/animspeed;
	turn rfoot to x-axis <5> speed <150.0>/animspeed;
	turn pelvis to z-axis <-2> speed <20>/animspeed;
	
	sleep 100*animspeed;
	return (0);
}

walklegs()
{
	//step 1, foot back a bit
	
	turn rthigh to z-axis <-2> speed <20.0>/animspeed;
	turn rthigh to x-axis <22> speed <220.0>/animspeed;
	turn rleg to x-axis <1> speed <10.0>/animspeed;
	turn rfoot to x-axis <-23> speed <230.0>/animspeed;
	
	turn lthigh to z-axis <2> speed <20.0>/animspeed;
	turn lthigh to x-axis <-42> speed <740.0>/animspeed;
	turn lleg to x-axis <-22> speed <500.0>/animspeed;
	turn lfoot to x-axis <14> speed <650.0>/animspeed;
	move pelvis to y-axis [1] speed [10.0]/animspeed;
	
	sleep 100*animspeed;
	
	
	//step 2, foot fully back, heel lifted
	
	
	//turn rthigh to z-axis <-2> now;
	turn rthigh to x-axis <34> speed <120.0>/animspeed;
	turn rleg to x-axis <23> speed <220.0>/animspeed;
	turn rfoot to x-axis <-32> speed <90.0>/animspeed;
	
	turn lthigh to x-axis <10> speed <520.0>/animspeed;
	turn lleg to x-axis <-48> speed <260.0>/animspeed;
	turn lfoot to x-axis <43> speed <290.0>/animspeed;
	move pelvis to y-axis [0] speed [10.0]/animspeed;
	
	sleep 100*animspeed;
	
	//step 3, foot lifted, knee bent
	
	turn rthigh to z-axis <0> speed <20.0>/animspeed;
	turn rthigh to x-axis <16> speed <180.0>/animspeed;
	turn rleg to x-axis <28> speed <50.0>/animspeed;
	turn rfoot to x-axis <-51> speed <190.0>/animspeed;
	
	turn lthigh to z-axis <0> speed <20.0>/animspeed;
	turn lthigh to x-axis <0> speed <100.0>/animspeed;
	turn lleg to x-axis <0> speed <480.0>/animspeed;
	turn lfoot to x-axis <0> speed <430.0>/animspeed;
	move pelvis to y-axis [0] now;
	
	sleep 100*animspeed;
	
	//step 4 foot lifted, knee extended
	
	turn rthigh to z-axis <2> speed <20.0>/animspeed;
	turn rthigh to x-axis <-21> speed <370.0>/animspeed;
	turn rleg to x-axis <-22> speed <500.0>/animspeed;
	turn rfoot to x-axis <14> speed <650.0>/animspeed;
	
	turn lthigh to z-axis <-2> speed <20.0>/animspeed;
	turn lthigh to x-axis <44> speed <440.0>/animspeed;
	turn lleg to x-axis <1> speed <10.0>/animspeed;
	turn lfoot to x-axis <-23> speed <230.0>/animspeed;
	move pelvis to y-axis [1] speed [10.0]/animspeed;
	
	sleep 100*animspeed;
	
	//step 5, foot on ground, knee extended
	
	
	// turn rthigh to z-axis <5> now;
	turn rthigh to x-axis <5> speed <260.0>/animspeed;
	turn rleg to x-axis <-48> speed <260.0>/animspeed;
	turn rfoot to x-axis <43> speed <290.0>/animspeed;
	
	//turn lthigh to z-axis <-2> now;
	turn lthigh to x-axis <68> speed <240.0>/animspeed;
	turn lleg to x-axis <23> speed <220.0>/animspeed;
	turn lfoot to x-axis <-32> speed <90.0>/animspeed;
	move pelvis to y-axis [0] speed [10.0]/animspeed;
	
	sleep 100*animspeed;
	
	//Step 0, all back to default
	
	turn rthigh to z-axis <0> speed <20.0>/animspeed;
	turn rthigh to x-axis <0> speed <50.0>/animspeed;
	turn rleg to x-axis <0> speed <480.0>/animspeed;
	turn rfoot to x-axis <0> speed <430.0>/animspeed;
	
	turn lthigh to z-axis <0> speed <20.0>/animspeed;
	turn lthigh to x-axis <32> speed <360.0>/animspeed;
	turn lleg to x-axis <28> speed <50.0>/animspeed;
	turn lfoot to x-axis <-51> speed <190.0>/animspeed;
	move pelvis to y-axis [0] now;
	
	sleep 100*animspeed;

}

Movement(moving, aiming, justmoved)
{
	justmoved = TRUE;
	while( TRUE )
	{
		moving = isMoving;
		aiming = isAiming;
		if( moving )
		{
			if( aiming )
			{
				call-script walk();
			}
			if( !aiming )
			{
				call-script walk();
			}
			justmoved = TRUE;
		}
		if( !moving )
		{
			if( justmoved )
			{
						
				turn rthigh to z-axis <0> speed <20.0>;
				turn rthigh to x-axis <0> speed <50.0>;
				turn rleg to x-axis <0> speed <480.0>;
				turn rfoot to x-axis <0> speed <430.0>;
						
				turn lthigh to z-axis <0> speed <20.0>;
				turn lthigh to x-axis <0> speed <100.0>;
				turn lleg to x-axis <0> speed <480.0>;
				turn lfoot to x-axis <0> speed <430.0>;
				justmoved = FALSE;
			}
			sleep 100;
		}
	}
}


#define BASEPIECE pelvis
#define HITSPEED <45.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 8
#define MAXTILT 100

Create()
{
	hide lflare;
	hide rflare;
	hide aimx1;
	hide aimy1;
	isMoving = FALSE;
	isAiming = FALSE;
	miniguncount = 0;
	restore_delay = 3000;
	animspeed = 2.5;
	turn rsleeve to y-axis <5> now;
	turn lsleeve to y-axis <-5> now;
	restore_delay = 3000;
	start-script Movement();
}


StartMoving(reversing)
{
	isMoving = TRUE;
}

StopMoving()
{
	isMoving = FALSE;
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	turn body to y-axis <0.0> speed <120.0>;
	turn luparm to x-axis <0.0> speed <120.0>;
	turn ruparm to x-axis <0.0> speed <120.0>;
	spin rbarrel around z-axis speed <0.0>;
	spin lbarrel around z-axis speed <0.0>;
	isAiming = FALSE;
	wpn1_lasthead = 1000000;
}
SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	set-signal-mask SIGNAL_AIM1;
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

AimWeapon1(heading, pitch)
{
	signal SIGNAL_AIM1;
	isAiming = TRUE;
	turn body to y-axis heading speed <180.0>;
	turn luparm to x-axis 0 - pitch speed <360>;
	turn ruparm to x-axis 0 - pitch speed <360>;
	if (((get ABS(wpn1_lasthead - heading)) > 65536) OR(((get ABS(wpn1_lasthead - heading)) > 1300) AND ((get ABS(wpn1_lasthead - heading)) < 64236)))
	{
		wpn1_lasthead = 1000000;
		wait-for-turn body around y-axis;
		wait-for-turn luparm around x-axis;
	}
	wpn1_lasthead = heading;
	start-script RestoreAfterDelay();
	return (1);
}

FireWeapon1()
{
	spin rbarrel around z-axis speed <900.0>;
	spin lbarrel around z-axis speed <-900.0>;
	return(1);
}

Shot1(zero)
{
	signal SIGNAL_SHOOT1;
	set-signal-mask SIGNAL_SHOOT1;
	
	if(miniguncount == 0)
	{
		emit-sfx 1024 + 0 from rflare;
	}
	if(miniguncount == 1)
	{
		emit-sfx 1024 + 0 from lflare;
	}
	sleep 32;
	++miniguncount;
	if(miniguncount>=2)
	{
		miniguncount=0;
	}
	return(1);
}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = body;
}

QueryWeapon1(pieceIndex)
{
	if(miniguncount == 0)
	{
		pieceIndex = rflare;
	}
	if(miniguncount == 1)
	{
		pieceIndex = lflare;
	}
	return (0);
}



Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode pelvis type BITMAPONLY | NOHEATCLOUD;
		explode rthigh type BITMAPONLY | NOHEATCLOUD;
		explode rleg type BITMAPONLY | NOHEATCLOUD;
		explode rfoot type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode lfoot type BITMAPONLY | NOHEATCLOUD;
		explode body type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode pelvis type BITMAPONLY | NOHEATCLOUD;
		explode rthigh type FALL | NOHEATCLOUD;
		explode rleg type FALL | NOHEATCLOUD;
		explode rfoot type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode lleg type FALL | NOHEATCLOUD;
		explode lfoot type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode pelvis type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rthigh type SMOKE | FALL | NOHEATCLOUD;
		explode rleg type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rfoot type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode lthigh type SMOKE | FALL | NOHEATCLOUD;
		explode lfoot type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode body type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode pelvis type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rthigh type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode rleg type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode rfoot type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode lleg type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode lfoot type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode body type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
	return corpsetype;
}
