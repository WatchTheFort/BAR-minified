#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SIG_ANIM 2

piece  base, cover, converter;

static-var  mmState, Active;


activatescr()
{
	hide cover;
	//move converter to y-axis <6> speed <6>;
	sleep 1000;	
}

deactivatescr()
{
	//move converter to y-axis <0> speed <6>;
	sleep 1000;
	show cover;
}

Go()
{
	call-script activatescr();
	set ARMORED to 0;  //Not needed as MM in BAR have no damage modifyer tag set
}

Stop()
{
	set ARMORED to 1;
	call-script deactivatescr();
}

MovementControl()
{
	while (TRUE)
	{
		while (mmState == 1)
		{

			move converter to y-axis [12] speed [6.000000];
			sleep 2000;
		
			move converter to y-axis [0] speed [6.000000];
			sleep 2000;
		}
		sleep 100;
	}	
}

ImAMetalMaker()
{
	mmState = -1;
	while( TRUE )
	{
		if (Active == 1)
		{
			if (mmState != 1) {
				call-script Go();
				mmState = 1;
			}
		}
		else
		{
			if (mmState != 0) {
				call-script Stop();
				mmState = 0;
			}
		}
		sleep 1000;
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	start-script MovementControl();
	start-script ImAMetalMaker();
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

MMStatus(State)
{
	Active = State;
	//get PRINT(Active);
}

Create()
{
	start-script SmokeUnit();
}

SweetSpot(piecenum)
{
	piecenum = base;
}


Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode converter type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode converter type FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode converter type SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode converter type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}

