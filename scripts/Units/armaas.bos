
#include "exptype.h"

piece  flare1, flare2, missileflare1, missileflare2, base, missilebox1, missilebox2, turretgun1,
       turretgun2, gunbarrel1, gunbarrel2, ground, turretmis1, turretmis2;

static-var  gun_3, restore_delay, gun_2, oldHead;
static-var  Stunned;

// Signal definitions
#define SIGNAL_MOVE 1
#define SIGNAL_AIM2 512
#define SIGNAL_AIM3 1024


#define BASEPIECE base
#define HITSPEED <20.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 3
#define MAXTILT 100
#include "unit_hitbyweaponid_and_smoke.h"

RockOnWaves()
{
	var rockDir, rockAng, rockSpeed;
	rockDir = <-0.1>;
	rockAng = <-0.1>;
	while( TRUE )
	{
		while( get ABS(rockAng) < <1.31> )
		{
			rockSpeed = <1.45> - get ABS(rockAng);
			turn ground to x-axis rockAng speed rockSpeed*4;
			wait-for-turn ground around x-axis;
			rockAng = rockAng + rockDir;
		}
		rockDir = 0 - rockDir;
		rockAng = rockAng + rockDir + rockDir;
	}
}

Create()
{
	hide missileflare2;
	hide flare2;
	hide flare1;
	hide missileflare1;
	hide ground;
	hide flare2;
	hide flare1;
	gun_3 = flare1;
	gun_2 = flare1;
	restore_delay = 3000;
	start-script SmokeUnit();
	turn turretgun2 to x-axis <-25> speed <25.000000>;
    spin turretgun1 around y-axis speed <25.0>;
	turn missilebox1 to x-axis <-25.000000> speed <25.000000>;
	turn missilebox2 to x-axis <-25.000000> speed <25.000000>;	
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 500;
	}
	start-script RockOnWaves();
}

SetMaxReloadTime(reloadMS)
{
	restore_delay = reloadMS * 3;
}

ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	set-signal-mask 0;
	turn turretgun2 to x-axis <-25.000000> speed <25.000000>;
    spin turretgun1 around y-axis speed <25.0>;
	turn missilebox1 to y-axis <0.000000> speed <25.000000>;
	turn missilebox1 to x-axis <-25.000000> speed <25.000000>;
	turn missilebox2 to y-axis <0.000000> speed <25.000000>;
	turn missilebox2 to x-axis <-25.000000> speed <25.000000>;
}
Stop()
{
    spin turretgun1 around y-axis speed <0.0>;
}
SetStunned(State)
{
    Stunned = State;
	if (Stunned) {
	    call-script Stop();
	} else {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

waveSplash()
{
	while( TRUE )
	{
		turn base to x-axis <-3.0> speed <3.0>;
		move base to y-axis [0.23] speed [0.23];
		wait-for-turn base around x-axis;
		turn base to x-axis <0.0> speed <3.0>;
		move base to y-axis [0.0] speed [0.23];
		wait-for-turn base around x-axis;
	}
}

StartMoving(reversing)
{
	signal SIGNAL_MOVE;
	set-signal-mask SIGNAL_MOVE;
	var waketime;
	start-script waveSplash();
	while( TRUE )
	{
		emit-sfx 1024 + 1 from ground;
		waketime = (get CURRENT_SPEED) * 100 / get MAX_SPEED;
		if( waketime < 50 ) waketime = 50;
		sleep 14000 / waketime;
	}
}

StopMoving()
{
	signal SIGNAL_MOVE;
	turn base to x-axis <0.0> speed <3.0>;
	move base to y-axis [0.0] speed [0.23];
}

AimWeapon2(heading, pitch)
{
	signal SIGNAL_AIM2;
	set-signal-mask SIGNAL_AIM2;
	if( gun_2 )
	{
		turn missilebox2 to y-axis heading speed <455.000000>;
		turn missilebox2 to x-axis <0.000000> - pitch speed <430.000000>;
		wait-for-turn missilebox2 around y-axis;
		wait-for-turn missilebox2 around x-axis;
	}
	else
	{
		turn missilebox1 to y-axis heading speed <455.000000>;
		turn missilebox1 to x-axis <0.000000> - pitch speed <430.000000>;
		wait-for-turn missilebox1 around y-axis;
		wait-for-turn missilebox1 around x-axis;
	}
	start-script RestoreAfterDelay();
	return (1);
}

AimWeapon3(heading, pitch)
{
	signal SIGNAL_AIM3;
	set-signal-mask SIGNAL_AIM3;
	turn turretgun1 to y-axis heading speed <755.000000>;
	turn turretgun2 to x-axis <0.000000> - pitch speed <730.000000>;
	wait-for-turn turretgun1 around y-axis;
	wait-for-turn turretgun2 around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FireWeapon2()
{
	gun_2 = !gun_2;
}

FireWeapon3()
{
	if( gun_3 )
	{
	    emit-sfx 1024 + 0 from flare2;
		move gunbarrel2 to z-axis [-2.400000] speed [500.000000];
		sleep 100;
		wait-for-move gunbarrel2 along z-axis;
		move gunbarrel2 to z-axis [0.000000] speed [3.000000];
	}
	else
	{
	    emit-sfx 1024 + 0 from flare1;
		move gunbarrel1 to z-axis [-2.400000] speed [500.000000];
		sleep 100;
		wait-for-move gunbarrel1 along z-axis;
		move gunbarrel1 to z-axis [0.000000] speed [3.000000];
	}
	gun_3 = !gun_3;
}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = base;
}

AimFromWeapon2(pieceIndex)
{
	pieceIndex = missilebox1 + gun_2;
}

AimFromWeapon3(pieceIndex)
{
	pieceIndex = turretgun1;
}

QueryWeapon1(pieceIndex)
{
	pieceIndex = base;
}

QueryWeapon2(pieceIndex)
{
	pieceIndex = missileflare1 + gun_2;
}

QueryWeapon3(pieceIndex)
{
	pieceIndex = gun_3;
}

/*
AimWeapon4(heading, pitch)
{
	sleep 100;
	return (1);
}

FireWeapon4()
{
	sleep 100;
}

QueryWeapon4(pieceIndex)
{
	sleep 100;
}

AimFromWeapon4(pieceIndex)
{
	pieceIndex = base;
}
*/



Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode ground type BITMAPONLY | NOHEATCLOUD;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turretgun1 type BITMAPONLY | NOHEATCLOUD;
		explode turretgun2 type BITMAPONLY | NOHEATCLOUD;
		explode gunbarrel1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode ground type FALL | NOHEATCLOUD;
		explode turretgun1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turretgun2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode gunbarrel1 type FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode ground type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turretgun1 type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode turretgun2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode gunbarrel1 type SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode ground type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode base type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode turretgun1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turretgun2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode gunbarrel1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
