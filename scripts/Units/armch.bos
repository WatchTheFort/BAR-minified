
#include "exptype.h"

piece  base, beam, nanogun, doorr, doorl, post, wake, turret, cagelight, cagelight_emit;

static-var  readyToBuild, Wobble_status;

// Signal definitions
#define SIGNAL_MOVE 1
#define SIGNAL_BUILD 2
#define SIGNAL_IDLE 8

WobbleUnit()
{
	while( TRUE )
	{
		if( Wobble_status)
		{
			if( get GROUND_WATER_HEIGHT(get UNIT_XZ) < 0 ) //Over Water
			{
				move base to y-axis [-0.500000] speed [0.50000];
			}
			else
			{
				move base to y-axis [-0.500000] speed [0.50000];
			}
		}
		else
		{
			if( get GROUND_WATER_HEIGHT(get UNIT_XZ) < 0 ) //Over Water
			{
				move base to y-axis [0.500000] speed [0.50000];
			}
			else
			{
				move base to y-axis [0.000000] speed [0.50000];
			}
		}
		Wobble_status = !Wobble_status;
		sleep 260;
	}
}

BankControl()
{
	var lastHeading, currHeading, diffHeading;
	lastHeading = GET HEADING;
	while (TRUE)
	{
		currHeading = GET HEADING;
		diffHeading = currHeading - lastHeading;
		// Set values for tilt

		//Remove Extreme values
		if ( diffHeading > 1500 ) diffHeading = 1500;
		if ( diffHeading < -1500 ) diffHeading = -1500;
		//get PRINT(diffHeading);
		turn base to z-axis 1.0 * diffHeading speed <23>;
		
		lastHeading = currHeading;
		sleep 67;
	}
}


#define BASEPIECE base
#define HITSPEED <35.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 2
#define MAXTILT 100

#include "../unit_hitbyweaponid_and_smoke.h"

HoverIdle()
{
	signal SIGNAL_IDLE;
	set-signal-mask SIGNAL_IDLE;
	while( TRUE )
	{
	    emit-sfx 1024 + 2 from wake;
	    sleep 1750;
	}
}

StartMoving(reversing)
{
	signal SIGNAL_MOVE;
	set-signal-mask SIGNAL_MOVE;
	signal SIGNAL_IDLE;
	var waketime;
	var bowtime;
	while( TRUE )
	{
		emit-sfx 1024 + 0 from wake;
		if (bowtime) emit-sfx 1024 + 1 from wake;
		bowtime = !bowtime;
		waketime = (get CURRENT_SPEED) * 100 / get MAX_SPEED;
		if( waketime < 30 ) waketime = 30;
		sleep 15000 / waketime;
	}
}

StopMoving()
{
	signal SIGNAL_MOVE;
	signal SIGNAL_IDLE;
	start-script HoverIdle();
	return (0);
}

Create()
{
	hide wake;
	hide beam;
	hide cagelight_emit;
	readyToBuild = 1;

	while( get(BUILD_PERCENT_LEFT))
	{
		sleep 500;
	}
	start-script BankControl();
	start-script WobbleUnit();
}

Activate()
{

}

Deactivate()
{

}

StartBuilding(heading, pitch)
{	
	signal SIGNAL_BUILD;
	set-signal-mask SIGNAL_BUILD;

	if (!readyToBuild) {
		turn doorr to z-axis <-67> speed <99>;
		turn doorl to z-axis <67> speed <99>;
		wait-for-turn doorr around z-axis;

		move doorr to y-axis [-2.000000] speed [2.000000];
		move doorl to y-axis [-2.000000] speed [2.000000];
		wait-for-move doorr along y-axis;

		turn post to x-axis <90> speed <131>;
		turn nanogun to x-axis <-90> speed <131>;
		wait-for-turn post around x-axis;
		readyToBuild = TRUE;
	}

	turn post to y-axis heading speed <160.000000>;
	wait-for-turn post around y-axis;

	set INBUILDSTANCE to 1;

	show beam;
	show cagelight_emit;
	spin cagelight around y-axis speed <225> accelerate <2>;
}

StopBuilding()
{
	signal SIGNAL_BUILD;	
	set-signal-mask SIGNAL_BUILD;
	hide beam;
	hide cagelight_emit;
	stop-spin cagelight around y-axis decelerate <4>;

	sleep 6000;

	set INBUILDSTANCE to 0;
	readyToBuild = FALSE;

	
	turn post to y-axis <0> speed <160>;

	turn post to x-axis <0.000000> speed <197.000000>;
	turn nanogun to x-axis <0.000000> speed <197.000000>;
	wait-for-turn post around x-axis;

	move doorr to y-axis [0.000000] speed [4.000000];
	move doorl to y-axis [0.000000] speed [4.000000];
	wait-for-move doorr along y-axis;

	turn doorr to z-axis <0.000000> speed <143.000000>;
	turn doorl to z-axis <0.000000> speed <143.000000>;
	wait-for-turn doorr around z-axis;
}

QueryNanoPiece(pieceIndex)
{
	pieceIndex = beam;
}



Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode doorl type BITMAPONLY | NOHEATCLOUD;
		explode post type BITMAPONLY | NOHEATCLOUD;
		explode turret type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode doorl type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode doorr type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode doorl type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode doorr type SMOKE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode nanogun type SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode doorl type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode doorr type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode nanogun type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
