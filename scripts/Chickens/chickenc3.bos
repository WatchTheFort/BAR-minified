piece body,head,tail,rblade,lblade, flare, aimy1,
		lfthigh, lbthigh, rfthigh, rbthigh, 
		lfknee, lbknee, rfknee, rbknee, 
		lfshin, lbshin, rfshin, rbshin, 
		lffoot, lbfoot, rffoot, rbfoot;


// Signal definitions
#define SIG_AIM				2
#define SIG_AIM_2			4
#define SIG_MOVE			16

#include "sfxtype.h"
#include "exptype.h"

static-var maxSpeed, animFramesPerKeyframe, bMoving, animSpeed;

// For N:\animations\Chickens\chicken_walk_remaster_v2.blend Created by https://github.com/Beherith/Skeletor_S3O V((0, 3, 5))
#define MOVESCALE 40 //Higher values are bigger, 100 is default
#define MOVESPEED 3
#define animAmplitude 110
#define LUHAND lupperarm  //define these as the left and right head thingies
#define RUHAND rupperarm  
#define LLHAND lupperarm  //define these as the left and right head thingies
#define RLHAND rupperarm  
#define SIG_WALK 64 //these defines are needed before includes!
#include "chicken_walk_allterrain.h"


#define IDLEMOVESCALE 40
#define IDLEAMPLITUDE 100
#define IDLESPEED 15
#include "chicken_idle_allterrain.h"


#define DEATHSCALE 40 //Higher values are bigger, 100 is default
#define DEATHAMPLIDUTE 100 //Higher values are bigger, 100 is default
#define DEATHSPEED 10
#include "chicken_death_allterrain.h"
//use call-script DeathAnim(); from Killed()


#define BASEPIECE body //needed for HitByWeapon
#define HITSPEED <35.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 4
#define MAXTILT 100
#include "chicken_hitbyweapon.h"


// REMEMBER TO animspeed = 6 in Create() !!
UnitSpeed(){
	maxSpeed = get MAX_SPEED; // this returns cob units per frame i think
	animFramesPerKeyframe = MOVESPEED; //we need to calc the frames per keyframe value, from the known animtime
	maxSpeed = maxSpeed + (maxSpeed /(2*animFramesPerKeyframe)); // add fudge
	while(TRUE){
		animSpeed = (get CURRENT_SPEED);
		if (animSpeed<1) animSpeed=1;
		animSpeed = (maxSpeed * MOVESPEED) / animSpeed; 
		//get PRINT(maxSpeed, animFramesPerKeyframe, animSpeed); //how to print debug info from bos
		if (animSpeed<3) animSpeed=3;
		if (animspeed> 2* MOVESPEED) animSpeed = 2 * MOVESPEED;
		sleep 197;
	}
}
StartMoving(){
	signal SIG_WALK;
	bMoving=TRUE;
	start-script Walk();
}
StopMoving(){
	signal SIG_WALK;
	bMoving=FALSE;
	call-script StopWalking();
	start-script Idle();
}

Create()
{
	emit-sfx 1024 + 2 from body;
	animSpeed = MOVESPEED;
	start-script UnitSpeed();
	return (0);
}

SweetSpot(piecenum)
	{	piecenum=body;	}	
	
AimFromPrimary(piecenum)
	{	piecenum=head;	}

QueryPrimary(piecenum)
	{	piecenum=head; }

RestoreAfterDelay()
	{
	sleep 1000;
	turn aimy1 to y-axis <0> speed <180>;
	turn aimy1 to x-axis <0> speed <180>;
	}

AimPrimary(heading,pitch)
	{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	if (heading > <60> ) heading = <60>;
	if (heading < <-60> ) heading = <-60>;
	if (pitch > <60> ) pitch = <60>;
	if (pitch < <-60> ) pitch = <-60>;
	turn aimy1 to y-axis heading speed <250.000000>;
	turn aimy1 to x-axis <0.000000> - pitch speed <200.000000>;
		
	wait-for-turn aimy1 around y-axis;
	start-script RestoreAfterDelay();
	
	return(TRUE);
	}
	
Shot1()
	{
	turn lblade to y-axis <40> speed [1];
	turn rblade to y-axis <-40> speed [1];
	wait-for-turn lblade around y-axis;
	turn lblade to y-axis <0> speed [0.3];
	turn rblade to y-axis <0> speed [0.3];
	}
	

Killed( severity, corpsetype )
	{
	corpsetype = 1;
	emit-sfx 1025 from body;
	call-script DeathAnim();
	explode head type FALL | NOHEATCLOUD;
	explode tail type FALL | NOHEATCLOUD;
	explode lblade type FALL | NOHEATCLOUD;
	explode rblade type FALL | NOHEATCLOUD;
	return( 0 );
	}